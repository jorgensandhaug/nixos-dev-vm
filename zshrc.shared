# Dotfiles-managed zshrc
# On NixOS: autosuggestions, syntax-highlighting, zsh-vi-mode, starship are system-level
# On macOS: starship init below, others installed via brew if desired

# History
HISTSIZE=100000
SAVEHIST=100000
setopt SHARE_HISTORY HIST_IGNORE_ALL_DUPS HIST_IGNORE_SPACE HIST_REDUCE_BLANKS

# Navigation
setopt AUTO_CD AUTO_PUSHD PUSHD_IGNORE_DUPS
setopt EXTENDED_GLOB GLOB_DOTS INTERACTIVE_COMMENTS

# Completion
autoload -Uz compinit && compinit
zstyle ':completion:*' menu select
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}'
zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"
zstyle ':completion:*' special-dirs true

# Autojump (null_glob avoids errors on systems without nix store)
() {
  setopt local_options null_glob
  for f in /run/current-system/sw/share/autojump/autojump.zsh \
           /nix/store/*-autojump-*/share/autojump/autojump.zsh \
           /opt/homebrew/etc/profile.d/autojump.sh \
           /usr/share/autojump/autojump.zsh; do
    [ -f "$f" ] && source "$f" && break
  done
}

# Zsh plugins (on NixOS these are system-level, source here for macOS/other)
() {
  setopt local_options null_glob
  for f in /opt/homebrew/share/zsh-autosuggestions/zsh-autosuggestions.zsh \
           /usr/share/zsh-autosuggestions/zsh-autosuggestions.zsh; do
    [ -f "$f" ] && source "$f" && break
  done
  for f in /opt/homebrew/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh \
           /usr/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh; do
    [ -f "$f" ] && source "$f" && break
  done
}

# Aliases
alias lg="lazygit"
alias cc="claude --dangerously-skip-permissions"

# Starship prompt (on NixOS this is a no-op since programs.starship handles it)
command -v starship &>/dev/null && eval "$(starship init zsh)"
